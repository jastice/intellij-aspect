# Copyright 2026 JetBrains s.r.o.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@rules_java//java:defs.bzl", "JavaInfo")
load("@rules_java//java/common:java_plugin_info.bzl", "JavaPluginInfo")
load("//common:artifact_location.bzl", "artifact_location")
load("//common:common.bzl", "intellij_common")
load("//common:copy.bzl", "copy")
load("//common:dependencies.bzl", "intellij_deps")
load("//common:make_variables.bzl", "expand_make_variables")
load("//common:output_groups.bzl", "intellij_output_groups")
load("//common:provider.bzl", "intellij_provider")
load(":java_toolchain_info.bzl", "JAVA_TOOLCHAIN_TYPE")
load(":module.bzl", "intellij_module")

COMPILE_TIME_DEPS = [
    "jars",
    "_jvm",
    "runtime_jdk",
]

EXPORTED_COMPILE_TIME_DEPS = [
    "exports",
]

TOOLCHAIN_DEPS = [
    "_java_toolchain",
]

RUNTIME_DEPS = [
    "runtime_deps",
]

# Rule kinds of java rules that do not provide JavaInfo (typically because you cannot build other java targets
# against them; e.g., a binary is only executed, but not interfaced with from java).
PROVIDERLESS_JAVA_RULES = ["java_binary", "java_test", "java_plugin"]

def _get_javacopts_from_context(ctx):
    javacopts_raw = getattr(ctx.rule.attr, "javacopts", [])
    if javacopts_raw == None:  # "javacopts" might exist in ctx.rule.attr as None
        javacopts_raw = []
    javacopts = expand_make_variables(ctx, True, javacopts_raw)
    add_exports = ["--add-exports=" + export + "=ALL-UNNAMED" for export in getattr(ctx.rule.attr, "add_exports", [])]
    add_opens = ["--add-opens=" + open + "=ALL-UNNAMED" for open in getattr(ctx.rule.attr, "add_opens", [])]
    return javacopts + add_exports + add_opens

def _get_javacopts(target, ctx):
    java_info = target[JavaInfo]
    compilation_info = getattr(java_info, "compilation_info", None)
    module_flags_info = getattr(java_info, "module_flags_info", None)
    if compilation_info != None and module_flags_info != None:
        javacopts = expand_make_variables(ctx, True, compilation_info.javac_options.to_list())

        # javacopts here contain --add-export flags already (https://github.com/bazelbuild/rules_java/blob/faaab4062f81deefaeef76dd21b2a5212432f8e3/java/private/java_common_internal.bzl#L159)
        add_opens = ["--add-opens=" + open + "=ALL-UNNAMED" for open in module_flags_info.add_opens.to_list()]
        return javacopts + add_opens
    return _get_javacopts_from_context(ctx)

def _source_jars(output):
    if hasattr(output, "source_jars"):
        source_jars = output.source_jars
        if type(source_jars) == "depset":
            return source_jars.to_list()
        else:
            # assuming it returns sequence type here
            return source_jars
    if hasattr(output, "source_jar") and output.source_jar != None:
        return [output.source_jar]
    return []

def _get_jvm_outputs(target):
    return [
        intellij_common.struct(
            binary_jars = [artifact_location.from_file(output.class_jar)] if output.class_jar else [],
            interface_jars = [artifact_location.from_file(output.compile_jar)] if output.compile_jar else [],
            source_jars = [artifact_location.from_file(f) for f in _source_jars(output)],
        )
        for output in target[JavaInfo].java_outputs
    ]

def _has_api_generating_plugins(target):
    if JavaPluginInfo in target:
        return len(target[JavaPluginInfo].api_generating_plugins.processor_classes.to_list()) > 0
    return False

def _get_generated_jars(target):
    return [
        struct(
            binary_jars = [artifact_location.from_file(output.generated_class_jar)],
            source_jars = [artifact_location.from_file(output.generated_source_jar)],
        )
        for output in target[JavaInfo].java_outputs
        if (output != None) and (output.generated_class_jar != None)
    ]

def _get_outputs(target, ctx, jdeps):
    generated_outputs = [
        output
        for output in target[JavaInfo].java_outputs
        if (output != None) and (output.generated_class_jar != None)
    ]
    resolve_files = (
        [output.generated_class_jar for output in generated_outputs] +
        [output.generated_source_jar for output in generated_outputs]
    )
    # The transitive runtime and compile classpaths are not added here: every dependency carrying
    # JavaInfo is visited by the aspect itself and contributes its own jars.
    resolve_transitives = []
    for out in target[JavaInfo].java_outputs:
        if getattr(out, "compile_jar", None):
            resolve_files += [out.compile_jar]
        elif getattr(out, "ijar", None):
            resolve_files += [out.ijar]
        if getattr(out, "class_jar", None):
            resolve_files += [out.class_jar]
        if getattr(out, "source_jars", None):
            if type(out.source_jars) == "depset":
                resolve_transitives += [out.source_jars]
            else:
                resolve_files += out.source_jars
    return {
        intellij_output_groups.SYNC: intellij_common.depset(
            [f for f in resolve_files if f.is_source],
        ),
        intellij_output_groups.BUILD: intellij_common.depset(
            [f for f in resolve_files if not f.is_source] + jdeps,
            transitive = resolve_transitives,
        ),
    }

def _get_jdeps(target, ctx):
    jdeps = [jo.jdeps for jo in target[JavaInfo].java_outputs if jo.jdeps != None]

    # See https://github.com/bazelbuild/bazel/pull/26898
    # --experimental_inmemory_jdeps_files is true by default, which means jdeps won't be stored on disk with remote execution.
    # So we just "materialize" the in-memory file by copying it onto disk.
    materialized_jdeps = []

    # The tags implicitly enter the action as "execution info" which is also part of the action equality. Therefore, we have to include
    # them in the hash included in the output path to avoid non-sharable actions with the same output.
    extra_action_key = ""
    for tag in ctx.rule.attr.tags:
        extra_action_key += str(abs(hash(tag))) + "_"
    extra_action_key += "_"
    for raw_jdeps_file in jdeps:
        materialized_jdeps_file = ctx.actions.declare_file(
            "materialized_" + str(abs(hash(extra_action_key + raw_jdeps_file.path))) + "_" + raw_jdeps_file.basename,
        )
        copy(
            ctx,
            raw_jdeps_file,
            materialized_jdeps_file,
            progress = "(IntelliJ Bazel plugin) Materializing jdeps %s" % raw_jdeps_file,
            mnemonic = "IJBazelPluginMaterializeJdeps",
        )
        materialized_jdeps.append(materialized_jdeps_file)
    return materialized_jdeps

def _implementation(target, ctx, attr):
    if not JavaInfo in target:
        if ctx.rule.kind in PROVIDERLESS_JAVA_RULES:
            # While we cannot obtain any information from the provider, we still have to
            # mark this target as a java target and take dependencies into account.
            return intellij_module.result(
                value = intellij_common.struct(
                    has_api_generating_plugins = _has_api_generating_plugins(target),
                ),
                dependencies = {
                    intellij_deps.COMPILE_TIME: intellij_deps.collect(ctx, COMPILE_TIME_DEPS),
                    intellij_deps.EXPORTED_COMPILE_TIME: intellij_deps.collect(ctx, EXPORTED_COMPILE_TIME_DEPS),
                    intellij_deps.RUNTIME: intellij_deps.collect(ctx, RUNTIME_DEPS),
                    intellij_deps.TOOLCHAIN: intellij_deps.collect(ctx, TOOLCHAIN_DEPS),
                },
            )

        return None

    jdeps = _get_jdeps(target, ctx)
    return intellij_module.result(
        outputs = _get_outputs(target, ctx, jdeps),
        value = intellij_common.struct(
            full_compile_jars = artifact_location.from_depset(target[JavaInfo].full_compile_jars),
            has_api_generating_plugins = _has_api_generating_plugins(target),
        ),
        dependencies = {
            intellij_deps.COMPILE_TIME: intellij_deps.collect(ctx, COMPILE_TIME_DEPS),
            intellij_deps.EXPORTED_COMPILE_TIME: intellij_deps.collect(ctx, EXPORTED_COMPILE_TIME_DEPS),
            intellij_deps.RUNTIME: intellij_deps.collect(ctx, RUNTIME_DEPS),
            intellij_deps.TOOLCHAIN: intellij_deps.collect(ctx, TOOLCHAIN_DEPS),
        },
        internal_value = intellij_common.struct(
            java_common = intellij_common.struct(
                jars = _get_jvm_outputs(target),
                generated_jars = _get_generated_jars(target),
                jdeps = [artifact_location.from_file(jdep) for jdep in jdeps],
                javac_opts = _get_javacopts(target, ctx),
                jvm_target = True,
            ),
            exports = intellij_common.attr_as_label_list(ctx, "exports"),
        ),
    )

_aspect = intellij_module.aspect(
    provider = intellij_provider.JavaInfo,
    implementation = _implementation,
    field = "java_provider",
)

module = intellij_module.define(
    file = "java_info",
    aspect = _aspect,
    toolchains = [JAVA_TOOLCHAIN_TYPE],
    aspect_providers = [JavaInfo],
    rulesets = ["JAVA"],
)
